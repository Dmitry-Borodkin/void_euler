{   v_import("imports.void");
}

{   enable_imports(); }


//---------------------------------------------------------------------
double = float(64);

sqrt: (double) ~> double;
ceil: (double) ~> double;


//---------------------------------------------------------------------
//N = 10;
  N = 2000000;

{   printf("N: %d\n", N); }

sieve = new bool[N];        defer delete[] sieve;


//---------------------------------------------------------------------
{   memset(sieve, 0, N);

    sieve[2] := true;

    for (i: &int := 3; i < N; i += 2)
    {
        sieve[i] := true;
    }

    R = (ceil(sqrt(N)) : int);

    for (i: &int := 3; i < R; i += 2)
    {
        if (sieve[i])
        {
            for (k: &int := i*i, i2 = 2*i; k < N; k += i2)
            {
                sieve[k] := false;
            }
        }
    }
}


//---------------------------------------------------------------------
{
    sum: &long := 2;

    for (p: &int := 3; p < N; p += 2)
    {
        if (sieve[p])   sum += p;
    }

    printf("sum: %ld\n", sum);
}


